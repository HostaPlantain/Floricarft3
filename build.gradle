plugins {
	id 'com.wynprice.cursemaven' version '2.1.1'
}

repositories {
	// JEI
	maven { url 'http://dvs1.progwml6.com/files/maven' }

	// TOP
	maven { url 'http://maven.tterrag.com/' }

	// Curios
	maven { url 'https://maven.theillusivec4.top/' }

	// CurseForge
	maven { url 'https://minecraft.curseforge.com/api/maven/' }
}

sourceSets.main {
	java.srcDirs project.projectDir.name
	resources.srcDirs project.projectDir.name
}

ext.configFile = file 'build.properties'
configFile.withReader {
	// Load config. It shall from now be referenced as simply config or project.config
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse prop
}

dependencies {
	compileOnly fg.deobf("mezz.jei:jei-${config.mc_ver}:${config.jei_ver}:api")
	runtimeOnly fg.deobf("mezz.jei:jei-${config.mc_ver}:${config.jei_ver}")

	compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${config.mc_ver_short}:${config.mc_ver_short}-${config.top_ver}:api")
	runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-${config.mc_ver_short}:${config.mc_ver_short}-${config.top_ver}")

	compileOnly fg.deobf("top.theillusivec4.curios:curios:FORGE-${config.mc_ver}-${config.curios_ver}:api")
	runtimeOnly fg.deobf("top.theillusivec4.curios:curios:FORGE-${config.mc_ver}-${config.curios_ver}")

	compile fg.deobf("curse.maven:mgui:${config.mgui_ver}")
	compile fg.deobf("curse.maven:tetra:${config.tetra_ver}")

	compile project(':Flora')
}

configurations {
	deployerJars
}

group "com.${config.author}.${config.id}"
version "${config.mc_ver}-${config.fc_ver}"

// Example for how to get properties into the manifest for reading by the runtime..
jar {
	manifest {
		attributes([
			"Specification-Title": "${config.id}",
			"Specification-Vendor": "${config.id}",
			"Specification-Version": "1",	// We are version 1 of ourselves
			"Implementation-Title": project.name,
			"Implementation-Version": "${version}",
			"Implementation-Vendor" : "${config.id}",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
		])
	}
}
